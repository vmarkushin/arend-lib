\import Function
\import Function.Meta
\import Meta
\import Paths
\import Paths.Meta

\class Functor (F : \Type -> \Type)
  | fmap \alias \infixl 4 <$> {A B : \Type} (f : A -> B) : F A -> F B
  | fmap-id {A : \Type} {y : F A} : fmap id y = y
  | fmap-comp {A B C : \Type} {f : A -> B} {g : B -> C} {y : F A} : fmap (g o f) y = fmap g (fmap f y)

\func IdFunctor : Functor id \cowith
  | fmap => __ __
  | fmap-id => idp
  | fmap-comp => idp

\func CompFunctor (F G : \Type -> \Type) {FuncF : Functor F} {FuncG : Functor G} : Functor (F o G) \cowith
  | fmap f => FuncF.fmap (FuncG.fmap f)
  | fmap-id {A} {y} => rewrite (fmap-id-pointfree {G} {_}) $ FuncF.fmap-id
  | fmap-comp {A} {_} {_} {f} {g} {y} => rewriteI FuncF.fmap-comp $ pmap (FuncF.fmap __ y) $ ext $ (\lam _ => FuncG.fmap-comp)
  \where
    \func fmap-id-pointfree {F : \Type -> \Type} {FuncF : Functor F} {A : \Type}: FuncF.fmap {A} id = id
      => ext $ (\lam f => $ rewrite FuncF.fmap-id idp)
